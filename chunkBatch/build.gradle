plugins {
    id("org.springframework.boot") version "3.4.1"
    id("io.spring.dependency-management") version "1.1.7"
    id("com.diffplug.spotless") version "6.22.0"
    id("nu.studer.jooq") version "9.0"
    id("java")
    id("project-report")
    id("jacoco")
    id("com.github.spotbugs") version "6.0.27"
}

repositories {mavenCentral()}

group = 'com.example.chunkBatch'
version = '0.0.1-SNAPSHOT'
description = 'chunkBatch'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

bootJar {
    archiveBaseName = 'chunkBatch'
    archiveVersion = '0.0.1'
}

// https://docs.spring.io/spring-boot/appendix/dependency-versions/coordinates.html
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.batch:spring-batch-core'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

// https://github.com/diffplug/spotless
spotless {
    java {
        googleJavaFormat()
        target 'src/**/*.java'
    }
}

spotbugs {
    ignoreFailures = true
}

defaultTasks 'tasks', 'spotlessApply', 'clean', 'check', 'jacocoTestReport', 'spotbugsMain', 'projectReport', 'bootJar'


// カスタムGradleタスクの追加
task runChunkBatchJob(type: JavaExec) {
    group = "application"
    description = "Runs the chunk batch job"
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("com.example.chunkBatch.ChunkBatchApplication")
    args = ["--spring.batch.job.name=chunkJob"]
}

// 異なる名前でバッチを実行するタスク
task runChunkBatchWithNames(type: JavaExec) {
    group = "application"
    description = "Runs the chunk batch job with different names"
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("com.example.chunkBatch.ChunkBatchApplication")
    args = ["--spring.batch.job.name=chunkJob", "--names=Spring,Java,Gradle,Maven,Kotlin"]
}
