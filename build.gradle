plugins {
    id "org.springframework.boot" version "3.4.1" apply false
    id "io.spring.dependency-management" version "1.1.7" apply false
    id "com.diffplug.spotless" version "6.22.0" apply false
    id "nu.studer.jooq" version "9.0" apply false
    id "com.github.spotbugs" version "6.0.27" apply false
}

allprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "project-report"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.diffplug.spotless"
    apply plugin: "com.github.spotbugs"

    group = "com.example"
    version = "0.0.1-SNAPSHOT"

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }
}

subprojects {
    apply plugin: "org.springframework.boot"

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter-batch"
        implementation "org.springframework.batch:spring-batch-core"
        runtimeOnly "com.h2database:h2"
        annotationProcessor "org.projectlombok:lombok"
        compileOnly "org.projectlombok:lombok"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "org.springframework.batch:spring-batch-test"
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.required = true
            csv.required = true
            html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
        }
    }

    spotless {
        java {
            googleJavaFormat()
            target "src/**/*.java"
        }
    }

    spotbugs {
        ignoreFailures = true
    }
}

// ルートプロジェクトの設定
apply plugin: "org.springframework.boot"

bootJar {
    enabled = false
}

jar {
    enabled = true
}

defaultTasks "tasks", "spotlessApply", "clean", "check", "jacocoTestReport", "spotbugsMain", "projectReport"
